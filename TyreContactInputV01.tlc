%% Copyright (c) 2011-2020 rFpro Limited, All Rights Reserved.
%%
%% NOTICE:  All information contained herein is, and remains the property of rFpro. The intellectual and technical concepts contained
%% herein are proprietary to rFpro and may be covered by U.S. and foreign patents, patents in process, and are protected by trade secret or copyright law.
%% Dissemination of this information or reproduction of this material is strictly forbidden unless prior written permission is obtained from rFpro.
%%
%% The copyright notice above does not evidence any actual or intended publication or disclosure of this source code, which includes information that is confidential
%% and/or proprietary, and is a trade secret, of rFpro.  ANY REPRODUCTION, DISTRIBUTION, PUBLIC PERFORMANCE, OR PUBLIC DISPLAY OF THIS SOURCE CODE
%% WITHOUT THE EXPRESS WRITTEN CONSENT OF RFPRO IS STRICTLY PROHIBITED, AND IN VIOLATION OF APPLICABLE LAWS AND INTERNATIONAL TREATIES.
%% THE RECEIPT OR POSSESSION OF THIS SOURCE CODE AND/OR RELATED INFORMATION DOES NOT CONVEY OR IMPLY ANY RIGHTS TO REPRODUCE,
%% DISCLOSE OR DISTRIBUTE ITS CONTENTS, OR TO MANUFACTURE, USE, OR SELL ANYTHING THAT IT MAY DESCRIBE, IN WHOLE OR IN PART.

%implements TyreContactInputV01 "C"

%function BlockTypeSetup(block, system) Output
  %<LibAddToCommonIncludes("InternalsPluginExtras.hpp")>
  %openfile externs
    extern bool wantsTyreContactUpdates;
    extern TyreContactPointDefnV01 simulinkTyreContact[];
    extern long numContactPoints;
    extern TyreDefinitionInputV01 simulinkTyreDef;
    extern HDContactPatchV01 simulinkTyreContactInput[4];
  %closefile externs
  %<LibCacheExtern(externs)>
%endfunction

%function Start(block, system) Output
  wantsTyreContactUpdates = true;

  %assign wheel_id = LibBlockParameterValue(P1, 0)-1
  simulinkTyreDef.mTyreDef[(int)%<wheel_id>].mNomRadius = %<LibBlockParameterValue(P3, 0)>;
  simulinkTyreDef.mTyreDef[(int)%<wheel_id>].mNomWidth = %<LibBlockParameterValue(P4, 0)>;

  %if LibBlockParameterDimensions(P2)[0] > 0
    for (unsigned lp=0; lp<%<LibBlockParameterDimensions(P2)[0]>; lp++)
    {
      simulinkTyreContact[numContactPoints].mTyreID = (long)%<wheel_id>;
      simulinkTyreContact[numContactPoints].mWeighting = %<LibBlockMatrixParameter(P2, "lp","",0, "","",0)>;
      simulinkTyreContact[numContactPoints].mPos.Set(
        %<LibBlockMatrixParameter(P2, "lp","",0, "","",1)>,
        %<LibBlockMatrixParameter(P2, "lp","",0, "","",2)>,
        %<LibBlockMatrixParameter(P2, "lp","",0, "","",3)>);
      numContactPoints++;
    }
  %else
    // Default to 1 point 
    simulinkTyreContact[numContactPoints].mTyreID = (long)%<wheel_id>;
    simulinkTyreContact[numContactPoints].mWeighting = 1.0;
    simulinkTyreContact[numContactPoints].mPos.Set(0.0, 0.0, 0.0);
    numContactPoints++;
  %endif
%endfunction

%function Outputs(block, system) Output
  %assign wheel_id = LibBlockParameterValue(P1, 0)-1

  %<LibBlockOutputSignal(0, "", "", 0)> = simulinkTyreContactInput[(int)%<wheel_id>].mSurfaceGain;
  %<LibBlockOutputSignal(1, "", "", 0)> = simulinkTyreContactInput[(int)%<wheel_id>].mHeight;

  %<LibBlockOutputSignal(2, "", "", 0)> = simulinkTyreContactInput[(int)%<wheel_id>].mNormal[0];
  %<LibBlockOutputSignal(2, "", "", 1)> = simulinkTyreContactInput[(int)%<wheel_id>].mNormal[1];
  %<LibBlockOutputSignal(2, "", "", 2)> = simulinkTyreContactInput[(int)%<wheel_id>].mNormal[2];

  %<LibBlockOutputSignal(3, "", "", 0)> = simulinkTyreContactInput[(int)%<wheel_id>].mSurfaceType;

  %assign NUM_CHARS = LibBlockOutputSignalDimensions(4)[0]
  memset(%<LibBlockOutputSignalAddr(4, "", "", 0)>, 0, %<NUM_CHARS>);
  strncpy((char *)%<LibBlockOutputSignalAddr(4, "", "", 0)>, simulinkTyreContactInput[(int)%<wheel_id>].mTerrainName, %<NUM_CHARS>);

  %<LibBlockOutputSignal(5, "", "", 0)> = simulinkTyreContactInput[(int)%<wheel_id>].mHDvalid;
  %<LibBlockOutputSignal(6, "", "", 0)> = simulinkTyreContactInput[(int)%<wheel_id>].mPatchCentre[0];
  %<LibBlockOutputSignal(6, "", "", 1)> = simulinkTyreContactInput[(int)%<wheel_id>].mPatchCentre[1];
  %<LibBlockOutputSignal(6, "", "", 2)> = simulinkTyreContactInput[(int)%<wheel_id>].mPatchCentre[2];
%endfunction
